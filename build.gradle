buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java'
    id "com.google.protobuf" version "0.9.4"
    id "com.github.davidmc24.gradle.plugin.avro" version "1.9.1"
}

group = 'io.confluent.developer'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        url 'https://packages.confluent.io/maven'
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

sourceSets {
    main {
        java {
            srcDirs 'src/main/java', 'generated-sbe'
        }
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.25.3'
    }
}

tasks.register('sbe', JavaExec) {
    systemProperty("sbe.output.dir","generated-sbe")
    systemProperty("sbe.xinclude.aware", "true")
    group = "Execution"
    description = "Runs the SBE tool to generate code from schemas"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = "uk.co.real_logic.sbe.SbeTool"
    args "src/main/resources/sbe-schema/stock-schema.xml"
}

compileJava {
    dependsOn(generateProto)
}

processResources {
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
}


dependencies {
    implementation 'org.apache.kafka:kafka-clients:3.7.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.0'
    implementation 'net.datafaker:datafaker:2.2.2'
    implementation 'org.openjdk.jmh:jmh-core:1.37'
    implementation 'org.openjdk.jmh:jmh-generator-annprocess:1.37'
    implementation 'com.google.protobuf:protobuf-java:3.25.3'
    implementation 'org.apache.avro:avro:1.11.3'
    implementation 'uk.co.real-logic:sbe-all:1.31.1'
    implementation 'com.esotericsoftware:kryo:5.6.0'

    implementation "io.confluent:kafka-avro-serializer:7.6.0"
    implementation "io.confluent:kafka-protobuf-serializer:7.6.0"
    implementation "io.confluent:kafka-json-serializer:7.6.0"
    implementation "io.confluent:kafka-schema-registry-client:7.6.0"
    implementation "io.confluent:kafka-protobuf-provider:7.6.0"

    implementation 'org.apache.logging.log4j:log4j-api:3.0.0-beta1'
    implementation 'org.apache.logging.log4j:log4j-core:3.0.0-beta1'
    implementation 'org.apache.logging.log4j:log4j-slf4j-impl:3.0.0-beta2'

    testImplementation platform('org.junit:junit-bom:5.10.2')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.openjdk.jmh:jmh-core:1.37'
    testImplementation 'org.openjdk.jmh:jmh-generator-annprocess:1.37'
}


test {
    useJUnitPlatform()
}
